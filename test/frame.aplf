r←frame;t
t←⎕NS⍬
t.data←#.data
t.frame←{
    f←⍺ data.frame ⍵
    f2←data.frame f
    f3←⍺ data.frame↑[1]⍵
    ##.assert 2≡/(↑[1]⍵)f.values f2.values f3.values:
    ##.assert 2≡/⍺ f.labels f2.labels f3.labels:
    f
}
t.bracket←{
    ##.assert ⍵.series≡⍵[]:
    ##.assert ⍵.values≡⍵[;]:
    ##.assert ⍺≡⍵[⍺].label:
    ##.assert ⍵[⍺].values≡↓[1]⍵[;⍺]:
    ⍵
}
t.setlabels←{
    ⍵.labels←⍺
    ##.assert ⍺≡⍵[].label:
    ⍵
}
t.setvalue←{
    f←⍵
    f[⊃⍺;⊃⌽⍺]←*v←(f[⊃⍺;⊃⌽⍺])
    ##.assert (↑[1]⍵[⊃⌽⍺].(values[⊃⍺]))≡*v:
    f
}
t.csv←{
    f←⍵ data.frame ⍺ ⍝ write
    g←data.frame ⍺   ⍝ read
    ##.assert f≡⍥{⍵.(labels values)}g:
    ##.assert ⎕NDELETE ⍺:
    ⍵
}
r←0
r←t.(f←(l←'other' 'another')setlabels'2' 'one'bracket'one' '2'frame(⍳5)(2×⍳5))
r←t.((2 4)l setvalue 3 l setvalue(2 4)(1↑l)setvalue 3(1↑l)setvalue f)
r←t.(('test_frame.csv')csv f)
r←1